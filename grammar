NAME    : [_a-zA-Z][_a-zA-Z0-9]*
NUMBER  : [0-9]+
FLOAT   : (([0-9]*\.[0-9]+)|([0-9]+\.))
STRING  : ("|')([^\r\n])*?[^\\]\1
BOOLEAN : (True|False)

======================================

start :: statementList
       | function

statementList :: statement ";" statementList
               | statement ";"
               | Null

statement :: "import" importList
           | relational
           | modifier varList
           | NAME "(" argList ")"
           | "prototype" NAME
           | "pause"
           | "clear"

importList :: NAME
            | NAME "." NAME
            | NAME "as" NAME
            | NAME "." NAME "as" NAME

relational :: expression
             | relational "==" expression
             | relational "!=" expression
             | relational "<" expression
             | relational ">" expression
             | relational "<=" expression
             | relational ">=" expression
             | relational "and" expression
             | relational "or" expression

expression :: term
            | expression "+" term
            | expression "-" term

term :: primary
      | term "*" primary
      | term "/" primary

primary :: datatype
         | NAME
         | "!" NAME
		 | NAME "=" relational
         | NAME "=" "None"
         | "(" relational ")"
         | "-" primary
         | "+" primary

datatype :: NUMBER
          | FLOAT
          | STRING
          | BOOLEAN

modifier :: "var"
          | "static"
          | "const"
          | "extern"

varList :: variable "," varList
         | variable

variable :: NAME
          | NAME "=" expression
          | NAME "=" "[" valueList "]"
          | NAME "=" "(" valueList ")"
          | NAME "=" "{" key-map "}"

valueList :: value "," valueList
           | value
           | Null

value :: datatype
       | NAME

key-map :: key ":" value "," key-map
         | key ":" value
         | Null

key :: datatype
     | NAME

argList :: "var" variable "," argList
         | "var" variable

function :: "function" NAME "(" modifier varList ")" "{" block "}"

block :: block block
       | statementList
       | loopBlock
       | flowControl

loopBlock :: whileBlock
           | forBlock
           | foreachBlock

whileBlock :: "do" "{" block "}" "while" "(" expression ")" ";"
            | "while" "(" expression ")" "{" block "}"

forBlock :: "for" "(" expressionList ";" expression ";" expression ")" "{" block "}"

foreachBlock :: "foreach" "(" NAME "as" NAME ")" "{" block "}"

flowControl :: "if" "(" expression ")" "{" block "}" elif

elif :: "elif" "(" expression ")" "{" block "}" else
      | "else if" "(" expression ")" "{" block "}" else
      | Null

else :: "else" "{" block "}"
      | Null
